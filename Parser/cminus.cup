/*
  Created By: Arash Esfandiari and Josh McCaskil
  File Name: cminus.cup
  To Build: java java_cup.Main < cminus.cup
*/
   
   
/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Import the class java_cup.runtime.*  */
//import java_cup.runtime.*;
import absyn.*;
import absyn.Decs.*;
import absyn.Exps.*;
import absyn.Lists.*;
import absyn.VarDecs.*;
import absyn.Vars.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }

:};
   
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal ELSE, IF, INT, RETURN, VOID, WHILE;
terminal PLUS, MINUS, TIMES, DIV, LT, LTEQ, GT, GTEQ, EQ, NEQ, ASSIGN, SEMI, COMMA, LPAREN, RPAREN, LBRACKET, RBRACKET, LBRACE, RBRACE;
terminal String ID, NUM; 
terminal ERROR;

non terminal NameTy type_specifier;
non terminal DecList program, declaration_list;
non terminal VarDecList params, param_list, local_declarations;
non terminal ExpList statement_list, args, arg_list; 
non terminal Exp statement, expression_stmt, expression;
non terminal VarExp var;
non terminal IfExp selection_stmt;
non terminal WhileExp iteration_stmt; 
non terminal ReturnExp return_stmt; 
non terminal CompoundExp compound_stmt;
non terminal CallExp call; 
non terminal Dec declaration;
non terminal VarDec var_declaration, param;
non terminal FunctionDec fun_declaration;   

precedence right ASSIGN;
precedence nonassoc LT, GT, LTEQ, GTEQ, EQ, NEQ;
precedence left PLUS, MINUS;
precedence left DIV, TIMES;
precedence left ELSE;

/* ----------------------------Grammar Section-------------------- */




program                 ::= declaration_list:d
                            {: RESULT = d; :}
                          | ERROR
                            {:RESULT = null;:}
                          ;
declaration_list        ::= declaration_list:dl declaration:d
                            {: 
                            DecList l = dl;
                            while( l.tail != null )
                                l = l.tail;
                            l.tail = new DecList(d, null);
                            RESULT = dl;
                            :}
                          | declaration:d
                            {: RESULT = new DecList(d, null); :}
                          | error:e
                            {:
                              parser.report_error ("Invalid program, syntax error in declaration (declaration_list)", e);
                              RESULT = new DecList(null, null);
                            :}
                          ;
declaration             ::= var_declaration: vd 
                            {: RESULT = vd; :}
                          | fun_declaration:fd
                            {: RESULT = fd; :}
                          ;
var_declaration         ::= type_specifier:ts ID:i SEMI 
                            {: RESULT = new SimpleDec(tsleft, tsright, ts, i); :}
                          | type_specifier:ts ID:i LBRACKET NUM:n RBRACKET SEMI
                            {: RESULT = new ArrayDec(tsleft, tsright, ts, i, (new IntExp(nleft, nright, Integer.parseInt(n)))); :}
                          | type_specifier ID:i LBRACKET error:e RBRACKET SEMI
                            {: parser.report_error("Invalid variable declaration syntax", e); :}
                          | type_specifier error:e SEMI
                            {: parser.report_error("Invalid variable declaration syntax", e); :}
                          ;
type_specifier          ::= INT:i
                            {: RESULT = new NameTy(ileft, iright, NameTy.INT); :}
                          | VOID:v
                            {: RESULT = new NameTy(vleft, vright, NameTy.VOID); :}
                          ;
fun_declaration         ::= type_specifier:ts ID:i LPAREN params:p RPAREN compound_stmt:cs
                            {: RESULT = new FunctionDec(tsleft, tsright, ts, i, p, cs); :}
                          | error:e ID:i LPAREN params:p RPAREN compound_stmt:cs
                            {: 
                                parser.report_error("Invalid type_specifier syntax in function declaration", e);
                                RESULT = new FunctionDec(eleft, eright, new NameTy(eleft, eright, NameTy.VOID), i, p, cs); 
                            :}
                          | type_specifier:ts ID:i LPAREN params:p RPAREN error:e
                            {: 
                                parser.report_error("Invalid compound statement in function declaration", e);
                                RESULT = new FunctionDec(tsleft, tsright, ts, i, p, null); 
                            :}
                          ;
params                  ::= param_list:pl
                            {: RESULT = pl; :}
                          | VOID:v
                            {: RESULT = new VarDecList(new SimpleDec(vleft, vright, new NameTy(vleft, vright, NameTy.VOID), ""), null); :}
                          ;
param_list              ::= param_list:pl COMMA param:p
                            {: 
                                VarDecList l = pl;
                                while( l.tail != null )
                                    l = l.tail;
                                l.tail = new VarDecList(p, null);
                                RESULT = pl;
                            :}
                          | param:p
                            {: RESULT = new VarDecList(p, null); :}
                          | error:e
                            {:
                                parser.report_error("Invalid syntax in parameters of function declaration", e); 
                                RESULT = new VarDecList(new SimpleDec(eleft, eright, null, "ERROR"), null);
                            :}
                          ;
param                   ::= type_specifier:ts ID:i 
                            {: RESULT = new SimpleDec(tsleft, tsright, ts, i); :}
                          | type_specifier:ts ID:i LBRACKET RBRACKET
                          {: RESULT = new ArrayDec(tsleft, tsright, ts, i, null); :}
                          ;
compound_stmt           ::= LBRACE local_declarations:ld statement_list:sl RBRACE
                            {: RESULT = new CompoundExp(ldleft, ldright, ld, sl); :}
                          ;
local_declarations      ::= local_declarations:ld var_declaration:vd
                            {:	
                                VarDecList l = ld;
                                while(l.tail != null)
                                    l = l.tail;
                                l.tail = new VarDecList(vd, null);
                                RESULT = ld;
                            :}
                          | 
                            {:RESULT = new VarDecList(null, null);:}
                          ;
statement_list          ::= statement_list:sl statement:s
                            {:	
                                ExpList l = sl;
                                while(l.tail != null)
                                    l = l.tail;
                                l.tail = new ExpList(s, null);
                                RESULT = sl;
                            :}
                          | 
                            {:RESULT = new ExpList(null, null);:}
                          ;
statement               ::= expression_stmt:es
                            {: RESULT = es; :}
                          | compound_stmt:cs
                            {: RESULT = cs; :}
                          | selection_stmt:ss 
                            {: RESULT = ss; :}
                          | iteration_stmt:is
                            {: RESULT = is; :}
                          | return_stmt:rs
                            {: RESULT = rs; :}
                          ;
expression_stmt         ::= expression:e SEMI 
                            {: RESULT = e; :}
                          | SEMI:s
                            {: RESULT = new NilExp(sright, sleft); :}
                          | error:e SEMI
                            {: parser.report_error("Invalid expression statement ", e); :}
                          ;
selection_stmt          ::= IF LPAREN expression:e RPAREN statement:s
                            {: RESULT = new IfExp(eleft, eright, e, s, new NilExp(eleft, eright)); :}
                          | IF LPAREN expression:e RPAREN statement:s1 ELSE statement:s2
                            {: RESULT = new IfExp(eleft, eright, e, s1, s2); :}
                          | IF:i LPAREN error:e RPAREN statement:s
                            {: parser.report_error("Invalid if statement structure", e);
                               RESULT = new IfExp(ileft, iright, new NilExp(eleft, eright), s, new NilExp(sleft, sright));
                            :}
                          | IF:i LPAREN error:e RPAREN statement:s ELSE statement:s2
                            {: parser.report_error("Invalid if statement structure", e);
                               RESULT = new IfExp(ileft, iright, new NilExp(eleft, eright), s, s2);
                            :}
                          ;
iteration_stmt          ::= WHILE LPAREN expression:e RPAREN statement:s
                            {: RESULT = new WhileExp(eleft, eright, e, s); :}
                          | WHILE:w LPAREN error:e RPAREN statement:s
                            {: 
                                parser.report_error("Invalid expression in while loop", e); 
                                RESULT = new WhileExp(wleft, wright, null, s);
                            :}
                          | WHILE:w ERROR:err expression:e RPAREN statement:s
                            {: 
                                parser.report_error("Missing left parenthesis in while loop", err); 
                                RESULT = new WhileExp(wleft, wright, e, s);
                            :}
                          | WHILE:w LPAREN expression:e ERROR:err statement:s
                            {: 
                                parser.report_error("Missing right parenthesis in while loop", err); 
                                RESULT = new WhileExp(wleft, wright, e, s);
                            :}
                          ;
return_stmt             ::= RETURN expression:e SEMI 
                            {: RESULT = new ReturnExp(eleft, eright, e); :}
                          | RETURN SEMI:s
                            {: RESULT = new ReturnExp(sleft, sright, new NilExp(sleft, sright)); :}
                          ;
expression              ::= var:v ASSIGN expression:e
                            {: RESULT = new AssignExp(vleft, vright, v, e); :}
                          | expression:s1 PLUS expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.PLUS, s2); :}
                          | expression:s1 MINUS expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.MINUS, s2); :}
                          | expression:s1 TIMES expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.TIMES, s2); :}
                          | expression:s1 DIV expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.DIV, s2); :}
                          | expression:s1 EQ expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.EQ, s2); :}
                          | expression:s1 LT expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.LT, s2); :}
                          | expression:s1 GT expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.GT, s2); :}
                          | expression:s1 GTEQ expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.GTEQ, s2); :}
                          | expression:s1 LTEQ expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.LTEQ, s2); :}
                          | expression:s1 NEQ expression:s2
                            {: RESULT = new OpExp(s1left, s1right, s1, OpExp.NEQ, s2); :}
                          | LPAREN expression:s1 RPAREN
                            {: RESULT = s1; :}
                          | var:v
                            {: RESULT = v; :}
                          | call:c
                            {: RESULT = c; :}
                          | NUM:n
                            {: RESULT = new IntExp(nleft, nright, Integer.parseInt(n)); :}
                            ;
                            
var                     ::= ID:i
                            {: RESULT = new VarExp(ileft, iright, new SimpleVar(ileft, iright, i)); :}
                          | ID:i LBRACKET expression:e RBRACKET
                            {: RESULT = new VarExp(ileft, iright, new IndexVar(ileft, iright, i, e)); :}
                          | ID:i LBRACKET error:e RBRACKET
                            {: 
                                parser.report_error ("Invalid expression in id[expression]", e);
                                RESULT = new VarExp(ileft, iright, new IndexVar(ileft, iright, i, null)); 
                            :}
                          ;

call                    ::= ID:i LPAREN args:a RPAREN
                            {: RESULT = new CallExp(ileft, iright, i, a); :}
                          ;
args                    ::= arg_list:al
                            {: RESULT = al; :}
                          |
                            {: RESULT = null; :}
                          ;
arg_list                ::= arg_list:al COMMA expression:e
                            {:	
                                ExpList l = al;
                                while(l.tail != null)
                                    l = l.tail;
                                l.tail = new ExpList(e, null);
                                RESULT = al;
                            :} 
                          | expression:e
                            {: RESULT = new ExpList(e, null); :}
                          | error:e
                            {: parser.report_error("Invalid function arguments ", e); :}
                          ; 
                       